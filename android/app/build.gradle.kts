import java.util.Properties
import java.io.FileInputStream

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

// Load local.properties (if present)
val localProperties = Properties()
val localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.reader(Charsets.UTF_8).use { reader ->
        localProperties.load(reader)
    }
}

// Load key.properties (keystore)
val keystorePropertiesFile = rootProject.file("key.properties")
val keystoreProperties = Properties()
if (keystorePropertiesFile.exists()) {
    FileInputStream(keystorePropertiesFile).use { stream ->
        keystoreProperties.load(stream)
    }
}

android {
    namespace = "com.example.programgenie"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    // Kotlin DSL: add flavor dimension
    flavorDimensions += "app"

    defaultConfig {
        applicationId = "com.sqa.programgenie"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        create("development") {
            keystoreProperties.getProperty("developmentStoreFile")?.let { path ->
                storeFile = file(path)
            }
            storePassword = keystoreProperties.getProperty("developmentStorePassword")
            keyAlias = keystoreProperties.getProperty("developmentKeyAlias")
            keyPassword = keystoreProperties.getProperty("developmentKeyPassword")
        }

        create("release") {
            keystoreProperties.getProperty("productionStoreFile")?.let { path ->
                storeFile = file(path)
            }
            storePassword = keystoreProperties.getProperty("productionStorePassword")
            keyAlias = keystoreProperties.getProperty("productionKeyAlias")
            keyPassword = keystoreProperties.getProperty("productionKeyPassword")
        }
    }

    buildTypes {
        getByName("release") {
            isMinifyEnabled = true
            isShrinkResources = false
        }

        getByName("debug") {
            isMinifyEnabled = true
            isShrinkResources = false
        }
    }
}

flutter {
    source = "../.."
}

// ----- BEGIN flavorDimensions (autogenerated by flutter_flavorizr) -----
apply(from = "flavorizr.gradle.kts")
// ----- END flavorDimensions (autogenerated by flutter_flavorizr) -----
